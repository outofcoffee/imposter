apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

ext {
    version_classgraph = '4.8.163'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
}

dependencies {
    implementation project(':core:imposter-api')
    implementation project(':core:config')

    implementation "io.github.classgraph:classgraph:$version_classgraph"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // test
    testImplementation "org.junit.jupiter:junit-jupiter-api:$version_junit_jupiter"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$version_junit_jupiter"
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: version_hamcrest
    testImplementation "org.apache.logging.log4j:log4j-core:$version_log4j"
    testImplementation "org.apache.logging.log4j:log4j-slf4j18-impl:$version_log4j"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar

            repositories {
                maven {
                    url = version.endsWith('SNAPSHOT') ? mavenSnapshotRepository : mavenReleaseRepository
                    credentials(AwsCredentials) {
                        accessKey awsAccessKey
                        secretKey awsSecretKey
                    }
                }
            }
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11

        // see https://kotlinlang.org/docs/java-to-kotlin-interop.html#default-methods-in-interfaces
        freeCompilerArgs = ["-Xjvm-default=all"]
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11

        // see https://kotlinlang.org/docs/java-to-kotlin-interop.html#default-methods-in-interfaces
        freeCompilerArgs = ["-Xjvm-default=all"]
    }
}

test {
    useJUnitPlatform()
}
