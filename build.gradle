import com.amazonaws.auth.profile.ProfileCredentialsProvider

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.amazonaws:aws-java-sdk-core:1.12.437'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.9.10' apply false
}

ext {
    kotlin_version = '1.9.10'

    try {
        awsCredentials = new ProfileCredentialsProvider().credentials
    } catch(ignored) {
        awsCredentials = null
    }

    mavenSnapshotRepository = 's3://gatehillsoftware-maven/snapshots'
    mavenReleaseRepository = 's3://gatehillsoftware-maven/releases'

    awsAccessKey = awsCredentials?.AWSAccessKeyId ?: System.getenv('AWS_ACCESS_KEY_ID') ?: project.findProperty('AWS_ACCESS_KEY_ID')
    awsSecretKey = awsCredentials?.AWSSecretKey ?: System.getenv('AWS_SECRET_ACCESS_KEY') ?: project.findProperty('AWS_SECRET_ACCESS_KEY')

    version_args4j = '2.33'
    version_aws_sdk = '1.12.578'
    version_coroutines = '1.7.3'
    version_hamcrest = '2.2'
    version_groovy = '4.0.13'
    version_guice = '5.1.0'
    version_jackson = '2.15.2'
    version_jackson_databind = '2.13.4.2'
    version_jaxb_api = '2.3.1'
    version_junit = '4.13.2'
    version_log4j = '2.18.0'
    version_micrometer = '1.9.4'
    version_mockito = '5.2.0'
    version_mockito_kotlin = '4.1.0'
    version_s3mock = '2.8.0'
    version_testcontainers = '1.17.6'
    version_vertx = '4.4.5'
}

allprojects {
    version project.property('projectVersion')
}

subprojects {
    group 'io.gatehill.imposter'

    buildscript {
        ext {
            version_shadow = '5.1.0'
        }
        repositories {
            mavenCentral()
            maven {
                url "https://plugins.gradle.org/m2/"
            }
        }
        dependencies {
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
            classpath "com.github.jengelman.gradle.plugins:shadow:$version_shadow"
        }
    }

    configurations {
        pluginImplementation.description = 'Only dependencies (and their transitive dependencies) using this configuration will be bundled in the plugin shadow JAR.'
        implementation.extendsFrom(pluginImplementation)
    }

    repositories {
        mavenCentral()
    }

    plugins.withType(JavaPlugin).whenPluginAdded {
        project.archivesBaseName = project.name.startsWith('imposter-') ? project.name : 'imposter-' + project.name
    }
}

wrapper {
    gradleVersion = '7.6.1'
}
