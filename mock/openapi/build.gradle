apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

ext {
    version_swagger_parser = '2.1.1'
    version_swagger_request_validator = '2.30.0'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
}

dependencies {
    implementation project(':core:imposter-engine')
    implementation project(':core:config-resolver-s3')

    implementation "io.swagger.parser.v3:swagger-parser:$version_swagger_parser"
    implementation "com.atlassian.oai:swagger-request-validator-core:$version_swagger_request_validator"

    implementation platform("com.amazonaws:aws-java-sdk-bom:$version_aws_sdk")
    implementation 'com.amazonaws:aws-java-sdk-s3'

    // test
    testImplementation project(':test:test-utils')
    testImplementation project(':test:api-tests')
    testImplementation "org.testcontainers:testcontainers:$version_testcontainers"
    testImplementation "com.adobe.testing:s3mock-testcontainers:$version_s3mock"

    // java 11 compatibility
    testImplementation ("javax.xml.bind:jaxb-api:$version_jaxb_api")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar

            repositories {
                maven {
                    url = version.endsWith('SNAPSHOT') ? mavenSnapshotRepository : mavenReleaseRepository
                    credentials(AwsCredentials) {
                        accessKey awsAccessKey
                        secretKey awsSecretKey
                    }
                }
            }
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11

        // see https://kotlinlang.org/docs/java-to-kotlin-interop.html#default-methods-in-interfaces
        freeCompilerArgs = ["-Xjvm-default=all"]
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11

        // see https://kotlinlang.org/docs/java-to-kotlin-interop.html#default-methods-in-interfaces
        freeCompilerArgs = ["-Xjvm-default=all"]
    }
}
