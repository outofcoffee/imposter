apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

ext {
    version_hbaseApi = '0.98.18-hadoop2'
}

compileJava {
    sourceCompatibility = 1.8
}

dependencies {
    implementation project(':core:imposter-engine')

    // hbase
    pluginImplementation("org.apache.hbase:hbase-client:$version_hbaseApi") {
        exclude module: 'slf4j-log4j12'
    }
    pluginImplementation("org.apache.hbase:hbase-rest:$version_hbaseApi") {
        exclude module: 'slf4j-log4j12'
    }

    // test
    testImplementation project(':test:test-utils')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar

            repositories {
                maven {
                    url = version.endsWith('SNAPSHOT') ? mavenSnapshotRepository : mavenReleaseRepository
                    credentials(AwsCredentials) {
                        accessKey awsAccessKey
                        secretKey awsSecretKey
                    }
                }
            }
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"

        // see https://kotlinlang.org/docs/java-to-kotlin-interop.html#default-methods-in-interfaces
        freeCompilerArgs = ["-Xjvm-default=all"]
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"

        // see https://kotlinlang.org/docs/java-to-kotlin-interop.html#default-methods-in-interfaces
        freeCompilerArgs = ["-Xjvm-default=all"]
    }
}

shadowJar {
    // override plugin archive name
    archiveBaseName.set("imposter-plugin-mock-hbase")
    archiveVersion.set('')
    archiveClassifier.set('')
    configurations = [project.configurations.pluginImplementation]
}

task dist {
    dependsOn shadowJar
}
