apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

ext {
    version_graal = '22.2.0.1'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
}

dependencies {
    implementation project(':core:imposter-engine')
    implementation project(':scripting:scripting-common')

    pluginImplementation "org.graalvm.js:js:$version_graal"
    pluginImplementation "org.graalvm.truffle:truffle-api:$version_graal"
    pluginImplementation "org.graalvm.js:js-scriptengine:$version_graal"

    // test
    testImplementation project(':test:test-utils')
    testImplementation project(':test:api-tests')

    // java 11 compatibility
    testImplementation ("javax.xml.bind:jaxb-api:$version_jaxb_api")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact shadowJar
            artifact sourcesJar

            repositories {
                maven {
                    url = version.endsWith('SNAPSHOT') ? mavenSnapshotRepository : mavenReleaseRepository
                    credentials(AwsCredentials) {
                        accessKey awsAccessKey
                        secretKey awsSecretKey
                    }
                }
            }
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11

        // see https://kotlinlang.org/docs/java-to-kotlin-interop.html#default-methods-in-interfaces
        freeCompilerArgs = ["-Xjvm-default=all"]
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11

        // see https://kotlinlang.org/docs/java-to-kotlin-interop.html#default-methods-in-interfaces
        freeCompilerArgs = ["-Xjvm-default=all"]
    }
}

shadowJar {
    // override plugin archive name
    archiveBaseName.set("imposter-plugin-js-graal")
    archiveVersion.set('')
    archiveClassifier.set('')
    configurations = [project.configurations.pluginImplementation]
}

task dist {
    dependsOn shadowJar
}
