apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
}

dependencies {
    api project(':core:imposter-engine')

    // scripting
    implementation project (':scripting:scripting-groovy')
    implementation project(':scripting:scripting-nashorn-standalone')

    // needed for plugin delegation
    implementation project(':scripting:scripting-common')
    api project(':store:store-common')
    api project(':store:store-inmem')

    // logging
    implementation "org.apache.logging.log4j:log4j-core:$version_log4j"
    implementation "org.apache.logging.log4j:log4j-jcl:$version_log4j"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:$version_log4j"
    implementation "org.apache.logging.log4j:log4j-1.2-api:$version_log4j"
    implementation "org.apache.logging.log4j:log4j-jul:$version_log4j"

    // test
    testImplementation project(':test:test-utils')
    testImplementation project(':test:api-tests')

    // java 11 compatibility
    testImplementation ("javax.xml.bind:jaxb-api:$version_jaxb_api")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar

            repositories {
                maven {
                    url = version.endsWith('SNAPSHOT') ? mavenSnapshotRepository : mavenReleaseRepository
                    credentials(AwsCredentials) {
                        accessKey awsAccessKey
                        secretKey awsSecretKey
                    }
                }
            }
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11

        // see https://kotlinlang.org/docs/java-to-kotlin-interop.html#default-methods-in-interfaces
        freeCompilerArgs = ["-Xjvm-default=all"]
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11

        // see https://kotlinlang.org/docs/java-to-kotlin-interop.html#default-methods-in-interfaces
        freeCompilerArgs = ["-Xjvm-default=all"]
    }
}
